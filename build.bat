@echo off
setlocal ENABLEDELAYEDEXPANSION
chcp 65001 > nul

:: Mask for powershell files
set srcDir=%cd%\src
set mask=%srcDir%\*.ps1

:: Create build folder if not exists
set outputDir=%cd%\build
mkdir "%outputDir%" 2> nul

set outputFile=%outputDir%\app.tmp
set outputBasename=app.ps1
set launcherFile=%outputDir%\launcher.vbs

:: Remove temp and old-build files
del "%outputDir%\*.ps1" 2> nul

echo [Builder] Current directory: %cd%
echo [Builder] Find files on mask %mask% ...

echo # PScript builder > %outputFile%
echo. >> %outputFile%

for /f %%f in ('dir "%mask%" /b/s') do (
	if not "%%~nf" == "index" if not "%%~nf" == "test" (
		echo [Builder] Import file: %%f
		echo # [Builder] Import %%~nf >> %outputFile%
		type %%f >> %outputFile%
		echo. >> %outputFile%
	)
)

if exist "%srcDir%\index.ps1" (
	echo [Builder] Import index.ps1 file
	echo. >> %outputFile%
	echo # [Builder] Import index.ps1 file ; >> %outputFile%
	echo. >> %outputFile%
	type "%srcDir%\index.ps1" >> %outputFile%
) else (
	if exist "%srcDir%\test.ps1" (
		echo [Builder] Import test.ps1 file
		echo. >> %outputFile%
		echo # [Builder] Import test.ps1 file ; >> %outputFile%
		echo. >> %outputFile%
		type "%srcDir%\test.ps1" >> %outputFile%
	)
)

rename "%outputFile%" "%outputBasename%"

if not exist "%launcherFile%" (
	echo ' generated by PSFrame builder > "%launcherFile%"
	echo set Shell = CreateObject("WScript.Shell"^) >> "%launcherFile%"
	echo set FSO = CreateObject("Scripting.FileSystemObject"^) >> "%launcherFile%"
	echo Path = "powershell.exe -ExecutionPolicy UnRestricted -WindowStyle hidden -File " ^& FSO.GetParentFolderName(FSO.GetFile(WScript.ScriptFullName^)^) ^& "\\%outputBasename%" >> "%launcherFile%"
	echo Shell.Run Path, 4, true >> "%launcherFile%"
)


if "%~1" == "-launch" (
	goto launch
)

set /p launch=Launch builded script [y/n]?: 

echo "%launch%"

if "%launch%"=="y" (
	:launch
	echo Script path = "%outputDir%\%outputBasename%"
	echo Application starting...
	powershell.exe -ExecutionPolicy UnRestricted -File "%outputDir%\%outputBasename%"
)